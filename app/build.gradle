apply plugin: 'com.android.application'
apply plugin: 'com.jaredsburrows.license'

android {
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "cz.vitskalicky.lepsirozvrh"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 13
        versionName "1.6.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        /**
         * Whether to allow automatic crash reporting using Sentry (managed by Vít Skalický).
         * Allow only for official release builds.
         */
        buildConfigField "Boolean", "ALLOW_SENTRY", "false"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            buildConfigField "Boolean", "ALLOW_SENTRY", "false"
        }
    }
    flavorDimensions "version"
    productFlavors {
        /**
         * For almost anything.
         *
         * // it is named "defalt" because "default" is a keyword.
         */
        defalt {
            dimension "version"
        }
        /**
         * For official fdroid releases only. Allows crash reports using Sentry, which is managed by Vít Skalický.
         * Crashes from your testing are not welcome there.
         */
        fdroid {
            dimension "version"
            buildConfigField "Boolean", "ALLOW_SENTRY", "true"
        }
        /**
         *  For Google play releases containing google in-app purchases only.
         *  Allows crash reports using Sentry, which is managed by Vít Skalický.
         *  Crashes from your testing are not welcome there.
         */
        play{
            dimension "version"
            buildConfigField "Boolean", "ALLOW_SENTRY", "true"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

}

licenseReport {
    generateHtmlReport = true
    generateJsonReport = false

    // These options are ignored for Java projects
    copyHtmlReportToAssets = true
    copyJsonReportToAssets = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.volley:volley:1.1.1'
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'org.simpleframework:simple-xml:2.7.1'
    implementation 'joda-time:joda-time:2.10.2'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'io.sentry:sentry-android:1.7.28'
    implementation 'org.slf4j:slf4j-nop:1.7.29'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
    implementation 'com.jaredrummler:cyanea:1.0.2'

    def room_version = "2.2.5"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    def paging_version = "2.1.2"

    implementation "androidx.paging:paging-runtime:$paging_version"

    playImplementation 'com.android.billingclient:billing:2.2.1'

}
