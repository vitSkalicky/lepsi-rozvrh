apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "cz.vitskalicky.lepsirozvrh"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        /**
         * Whether to allow automatic crash reporting using Sentry (managed by Vít Skalický).
         * Allow only for official release builds.
         */
        buildConfigField "Boolean", "ALLOW_SENTRY", "false"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
        /**
         * For official releases only. Allows crash reports using Sentry, which is managed by Vít Skalický.
         * Crashes from your testing are not welcome there.
         */
        releaseOfficial {
            initWith release
            buildConfigField "Boolean", "ALLOW_SENTRY", "true"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.volley:volley:1.1.1'
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'org.simpleframework:simple-xml:2.7.1'
    implementation 'joda-time:joda-time:2.10.2'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'io.sentry:sentry-android:1.7.28'
    implementation 'org.slf4j:slf4j-nop:1.7.29'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    def room_version = "2.2.3"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    def paging_version = "2.1.1"

    implementation "androidx.paging:paging-runtime:$paging_version"
}
